# vim: filetype=zsh
export XDG_CACHE_HOME="${HOME}/.cache"
export XDG_CONFIG_HOME="${HOME}/.config"
export XDG_DATA_HOME="${HOME}/.local/share"
export ELECTRON_OZONE_PLATFORM_HINT="auto"

# User Path
typeset -U path
path=("${HOME}/bin" "${HOME}/.local/bin" $path)
typeset -U fpath
fpath=("${HOME}/bin" $fpath)

# Colors
if [[ -z "${LS_COLORS}" ]]; then
    (( $+commands[dircolors] )) && eval "$(dircolors -b)"
fi

# Completion
autoload -Uz compinit && compinit -d "${XDG_CACHE_HOME}/.zcompdump-${HOST}"
zstyle ':completion:*' menu select
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' matcher-list 'm:{a-zA-Z-_}={A-Za-z_-}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*:(ssh|scp):*' hosts
zstyle ':completion:*:(ssh|scp):*' users

# History
HISTFILE="${XDG_CACHE_HOME}/.zsh_history"
HISTORY_IGNORE="(ls|cd|pwd|exit|ssh)*"
HISTSIZE=4000
SAVEHIST=$HISTSIZE
setopt hist_ignore_all_dups
setopt hist_ignore_space

# Editor
(( $+commands[vim] )) && export EDITOR='vim' || export EDITOR='nano'
export VISUAL="${EDITOR}"

# Aliases
alias e="${EDITOR}"
(( $+commands[git] )) && alias g='git'
alias j='journalctl -aq -u'
alias jcf='journalctl -aq --no-pager --output=cat -n 25 -f -u'
alias ll='ls -lh --color=auto'
alias ls='ls --color=auto'
(( $+commands[pass] )) && alias p='pass show'
(( $+commands[zbarimg] )) && alias qrdec='zbarimg -q --raw'
(( $+commands[qrencode] )) && alias qrenc='qrencode -t ansiutf8'
alias rip='dig TXT +short o-o.myaddr.l.google.com @ns1.google.com'
(( $+commands[rsync] )) && alias rs='rsync --archive --delete --progress --exclude={".git",".gitignore"} --filter=":- .gitignore"'
(( $+commands[shred] )) && alias sd='shred -u'
alias scu="systemctl --user"
alias t="${EDITOR} ~/todo.md"
(( $+commands[toolbox] )) && alias tbe="toolbox enter"
(( $+commands[toolbox] )) && alias tbr="toolbox run"
(( $+commands[rpm-ostree] )) && alias upgrade="rpm-ostree upgrade && flatpak upgrade --user --assumeyes"
(( $+commands[dnf] )) && alias upgrade="sudo dnf upgrade --refresh --assumeyes"

# ZSH-syntax-highlighting
if [[ -a ~/.submodules/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
    source ~/.submodules/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
    ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets cursor)
    ZSH_HIGHLIGHT_MAXLENGTH=250
    typeset -A ZSH_HIGHLIGHT_STYLES
    ZSH_HIGHLIGHT_STYLES[alias]='fg=magenta,bold'
    ZSH_HIGHLIGHT_STYLES[path]='fg=cyan'
    ZSH_HIGHLIGHT_STYLES[globbing]='none'
fi

# ZSH-autosuggestions
if [[ -a ~/.submodules/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
    source ~/.submodules/zsh-autosuggestions/zsh-autosuggestions.zsh
    ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
fi

# ZSH-history-substring-search
if [[ -a ~/.submodules/zsh-history-substring-search/zsh-history-substring-search.zsh ]]; then
    source ~/.submodules/zsh-history-substring-search/zsh-history-substring-search.zsh
    bindkey '^[[1;5A' history-substring-search-up
    bindkey '^[[1;5B' history-substring-search-down
fi

# Prompt Builder
function prompt_builder() {
    local s="%(?..%F{red}[%?]%f )%F{blue}%2~%f "
    # VCS Info
    s+="${vcs_info_msg_0_}"
    # Remote Session
    [[ -n "${SSH_CLIENT}" ]] && s+="%F{red} %f"
    # Toolbox Session
    [[ -n "${TOOLBOX_PATH}" ]] && s+="%F{cyan}⬢ %f"
    # Elevated Session
    s+="%(!.%F{red}#%f.%F{green}❯%f) "
    echo $s
}

# Prompt
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr ' %F{green}●%f'
zstyle ':vcs_info:*' unstagedstr ' %F{yellow}●%f'
zstyle ':vcs_info:git:*' formats '%F{green}%b%f%c%u '
zstyle ':vcs_info:git:*' actionformats '%F{green}%b (%a)%f%c%u '
setopt PROMPT_PERCENT
setopt PROMPT_SUBST
PROMPT='$(prompt_builder)'

# Start Sway
if [[ -z "$WAYLAND_DISPLAY" ]] && [[ "$XDG_VTNR" -eq 1 ]]; then
    exec sway
fi
